
[ -n LANG ] && export LANG=C.UTF-8

path+=~/.local/bin

# if using homebrew, put bin and sbin at start of path, setup fpath
if [[ -d /opt/homebrew ]]; then
    path=( /opt/homebrew/bin /opt/homebrew/sbin $path )
    fpath=( /opt/homebrew/share/zsh/site-functions $fpath )
fi

# add user completions to fpath
fpath+=( ~/.config/zsh/completions ~/.local/share/zsh/completions )

# functions
ZFUNCDIR=~/.config/zsh/functions
fpath+=$ZFUNCDIR
# glob all function names and autoload them
autoload -U $ZFUNCDIR/*(.:t)

# environment
export VIRTUAL_ENV_HOME="$HOME/.virtualenvs"

# set EDITOR and VISUAL if not set
if [[ -z $EDITOR ]]; then
    if [[ $TERM_PROGRAM = 'vscode' && $(command -v code) ]]; then
        export EDITOR='code --wait'
        export VISUAL="$EDITOR"
    elif [[ $(command -v vim) ]]; then
        export EDITOR=vim
        export VISUAL="$EDITOR"
    fi
fi

bindkey -e
bindkey '^[' vi-cmd-mode
autoload -U edit-command-line && zle -N edit-command-line
bindkey '^X^E' edit-command-line
# consider / a word boundary
export WORDCHARS=${WORDCHARS/\/}

setopt auto_cd
# history
HISTFILE=~/.zsh_history
SAVEHIST=100000; HISTSIZE=10000
setopt share_history
setopt extended_history
setopt interactive_comments
setopt hist_ignore_space

# starship prompt
if (( $+commands[starship] )); then
    eval "$(starship init zsh)"
fi

# direnv
if (( $+commands[direnv] )); then
    eval "$(direnv hook zsh)"
fi

# plugin names at ZPLUGINDIR
plugins=(
    zsh-abbr
    fzf-tab
    # keep the next three in this order
    zsh-syntax-highlighting
    zsh-autosuggestions
    zsh-history-substring-search
)

# load plugins
ZPLUGINDIR=~/.local/share/zsh/plugins
for plugin_name in $plugins; do
    local plugin_dir=$ZPLUGINDIR/$plugin_name
    local init_file=$plugin_dir/$plugin_name.plugin.zsh
    if [ ! -e $init_file ]; then
        echo >&2 "No init file found for plugin '$plugin_name'"; continue
    fi
    fpath+=$plugin_dir
    source $init_file
done

# use history substring search with up/down and C-p/C-n
if (( $+functions[history-substring-search-up] )); then
    bindkey '^P' history-substring-search-up; bindkey '^[[A' history-substring-search-up
    bindkey '^N' history-substring-search-down; bindkey '^[[B' history-substring-search-down
fi

# fzf
if (( $+commands[fzf] )); then
    export FZF_DEFAULT_OPTS='--layout=reverse'
    source $ZPLUGINDIR/fzf/shell/key-bindings.zsh
    if (( $+functions[fzf-tab-complete] )); then
        # zsh completion with tab, fzf completion with shift-tab
        bindkey '^I' expand-or-complete
        bindkey '^[[Z' fzf-tab-complete
    fi
fi

# setup colors
export CLICOLOR=1 # macos/bsd
if (( $+commands[dircolors] )); then
    eval "$(dircolors -b)"
elif (( $+commands[gdircolors] )); then
    eval "$(gdircolors -b)"
fi
if [ -v LS_COLORS ]; then 
    zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
fi

# load completion system
autoload -U compinit && compinit
autoload -U bashcompinit && bashcompinit
zstyle ':completion:*' menu select
zstyle ':completion:*' use-cache on
setopt complete_in_word 

# local config
if [ -f ~/.zshrc_local ]; then
    source ~/.zshrc_local
fi
